PREFIX schema: <https://schema.org/>
PREFIX skos: <https://www.w3.org/2004/02/skos/core#>
PREFIX capacoa:<https://capacoa.ca/vocabulary#>

INSERT {
    ?org schema:additionalType capacoa:PresenterType_Specialized .
    capacoa:PresenterType_Specialized a skos:Concept  .

    ?orgmulti schema:additionalType capacoa:PresenterType_Multidisciplinary .
    capacoa:PresenterType_Multidisciplinary a skos:Concept .
}
WHERE {
    {
        SELECT ?org 
        WHERE {
            ?org schema:additionalType capacoa:PrimaryActivity_pres .
            OPTIONAL {
                ?org schema:additionalType ?discipline .
                capacoa:Genres skos:member ?discipline .
            }
            FILTER NOT EXISTS {
                ?org schema:additionalType capacoa:PresentingFormat_occasional 
            }
        } 
        GROUP BY ?org
        HAVING (COUNT(?discipline) = 1)
    }
    UNION
    {
        SELECT ?orgmulti 
        WHERE {
            ?orgmulti schema:additionalType capacoa:PrimaryActivity_pres .
            OPTIONAL {
                ?orgmulti schema:additionalType ?discipline .
                capacoa:Genres skos:member ?discipline .
            }
            FILTER NOT EXISTS {
                ?orgmulti schema:additionalType capacoa:PresentingFormat_occasional 
            }
        } 
        GROUP BY ?orgmulti
        HAVING (COUNT(?discipline) > 1)
    }
}
